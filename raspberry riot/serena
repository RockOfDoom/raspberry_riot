

==serena==
VAR monsterQ_cycle = 0
Magical Seaglass Soldier Huckleberry, or Serena Linkle to her classmates.

16 years old. She's always in all of the plays at her high school, but everyone says the only people she really talks to are the other Raspberry Riot girls.

No external friends or social life to speak of, yet in the field she's generally considered the flashiest showgirl of the team.

Serena stalked into Lee's room, looking fairly tired. She was wearing a pale blue hoodie that's much too big for her, some worn-out jeans, and some unremarkable sneakers.

Before Isaac could even get a word in she silently moved to the center of the room, pulled a light purple crystal out of her hoodie pocket, and exclaimed "Magic Crystal Berry, Transformation!"

A bright teal-green light filled the room, and Serena lifted into the air glowing with power. She began to spin gracefully, like a ballet dancer, and her hair came unboundâ€”floating around her face like she was underwater. A sea-green ribbon tied itself into the back of her hair, and a shimmering sea glass necklace appeared around her neck. Water droplets appeared in the air around her, and flowed onto her body, sparkling and forming an ornate, lace-covered skirt and blouse set. Her legs were covered in tight white leggings, which were then covered with tiny gleaming clear beads; her shoes were replaced by a pair of small, rounded high heels with little teal ribbon-bows on the front. Finally, she threw her crystal into the air, where it twirled and transformed into a magical staff. She caught it as it fell, and twirled it as she returned to the ground.

When she touched down, Serena cheerfully spouted "Magical Seaglass Soldier Huckleberry, at your service!"

* [Thank Serena for the demonstration.] "That was a wonderful demonstration of your powers! Thank you."
    "Of course!" Serena, no, Huckleberry replied. She gave a brief curtsy.
    ->serena_choices
* [Tell Serena she didn't have to transform yet.] "Oh, I appreciate the thought but that won't be strictly necessary for this interview. If you would feel more comfortable out of costume, feel free." Isaac explained.
    "Well, I expect you came here to speak with Huckleberry, not Serena, yes? She IS the more interesting of the two."
    "I'm happy to speak with both, whichever way will make this easier for you." replied Isaac.
    "If it's the same either way," said Huckleberry, "I'd prefer to stay in costume."
    "That works for me." Isaac says.
    ->serena_choices
* [Refrain from commenting.] Isaac decided to not linger too long on the transformation and get right into the interview.
    -> serena_choices




==serena_choices===

* "How long have you been part of the team?"
    -> serena_q1
* "How did you become a magical girl?"
    -> serena_q2
* "How did you choose your magical girl name?"
    -> serena_q3
* "Can you explain your powers?"
    -> serena_q4
* "What do you think of the other girls?"
    -> serena_q5
* {monsterQ_cycle == 0} "How do you feel about always having to fight monsters in order to protect the City?"
    -> serena_q6
* {monsterQ_cycle == 1 && huckleberry >= is_lonely} "You're not afraid that you or your friends might get hurt?"
    -> serena_worriesMonsters
* {serena_choices > 1} "That's all the questions I had, thank you."
    -> serena_wrapup




==serena_q1===

"About 10 months, give or take." Huckleberry stated.

"That's not long after the alien attack on your school, was there some sort of connection?" the reporter gently prodded.

"Yeah." There was a long pause, like Huckleberry was deciding whether she even wanted to elaborate. Eventually, she continued: "I...got my powers towards the end of the attack, and then there were some openings so..."

She trailed off, and looked like she didn't want to continue.{team_trust >= tentative:
    "It's alright if you don't want to continue, I can tell this is a sensitive subject for you all." Isaac reassured Huckleberry.
    
    "Right, thank you..." mumbled Huckleberry.
    {move_to_state(team_trust, trusting)}
}

-> serena_choices




==serena_q2===

"It's...I dunno." Huckleberry shifted her skirt uncomfortably. "I don't really like to talk about it."

{team_trust < trusting:Isaac recognized a dead end when he saw one.}{team_trust == trusting:
    "That's ok, you only have to say as much as you want to." Isaac cracked a friendly grin, "Or, as little."
    
    "Hah, right." Huckleberry said. "It's just, the process of becoming a magical girl is," she paused, as if looking for words, "well, it's almost always somewhat...traumatic."
    
    "I see." said Isaac with some concern. "I won't push you about it, but I know some people you can talk to if you need help."
    
    "Oh! No, no, nothing like that!" said Huckleberry. "It's just, our magic is a response to a need, you know? An...expression of our emotions...uh...breaking out of us, and taking physical form, you know."
    
    Huckleberry started: "The kind of events that create a need so strong that someone develops their magical powers..."
    
    "Are unpleasant?" Isaac finished.
    
    "Yes, they are unpleasant." Huckleberry agreed.
}

-> serena_choices




==serena_q3===

Huckleberry stood up a little straighter and put on a radiant smile.

"Well, I had to come up with a lot of names to make sure the one I picked would fit with the team theme, you know?" she spouted with surprising energy. "Ultimately, 'Magical Seaglass Soldier Huckleberry' just fit the best between my powers and my space in the team. Plus, it's just fun to say, isn't it?"

As she said so, she made a small gesture that caused her glass necklace to sparkle and chime. The reporter suspected that this was on purpose.{huckleberry == is_the_culprit:
    
    * [Press her about the monsters.] "Is that all of your powers?"
        -> serena_confrontation
    * [Leave it be.] Isaac decided not to press Huckleberry about the monsters. Not now, at least.
        -> serena_choices
}

-> serena_choices




==serena_q4===

Huckleberry's glass necklace chimed as she gestured excitedly. "Well, my basic power is that I can control water. It's classic stuff, but if you know how to catch the light and make it sparkle it can still look pretty spectacular."

She straightened a little and claps her hands. When she separated them glittering teal water droplets filled the air between them. "But this is the really special stuff. I call them 'Lovely Crystal Tears', and I can make them.." she flourished her staff and a line of sparkling water was sketched into the air, "out of thin air!"

She leaned on her staff, and continued more casually: "I can use the Tears to heal the other girls, and they're also good for blocking magic and lasers and other things like that."

Huckleberry looked quite pleased with herself.

-> serena_choices



==serena_q5===

"Oh, they're all lovely," said Huckleberry.

"Strawberry is cute enough to kill, and not half bad at her job either. Everyone likes her, though, so nothing special there..." Huckleberry mused.

"Pepper can be a little standoffish but she's sweet once you get to know her, she always has your back when you need her." she continued.

"YoungOGSavage is definitely a character. I think we get the most questions about him, but he's fun to have around and he has a great work ethic."

"And Raspberry Jam. Well, we don't always see eye to eye about how the team should be run but," Huckleberry haughtily flipped some of her hair out of her face, "I still never doubt that he has everyone's best interests at heart."

-> serena_choices



==serena_q6===
{move_to_state(monster_knowledge, monsterKing_dead)}
~ monsterQ_cycle++
"Honestly?" Huckleberry started, "It's kind of exciting."

"It definitely used to be scarier, back when we were up against King Goregamesh, but ever since we handled him the monsters have been way less dangerous." She tapped her staff against the ground firmly, {monster_knowledge == girl_making_monsters:almost like she was trying to convince herself.}{monster_knowledge < girl_making_monsters:as if to emphasize her point.}

"But nowadays," she continued, "the guys we fight are kind of goofy. Last week, someone's pet lobster tripled in size and we had to catch it and return it to them." Huckleberry covered her mouth with the back of her hand to hold in a laugh. "They couldn't shrink it back, so they just got it a bigger tank! I guess some people will do anything for their pets...so strange."

"I still don't know what they did about the laser eyes though, that seems like that would make keeping it IN the tank much harder."

-> serena_choices



==serena_worriesMonsters===
{move_to_state(huckleberry, worries_abt_noMonsters)}
"Not at all, we're all plenty capable." Huckleberry stated confidently. "Honestly, in the weeks after our fight with Goregamesh I was more worried that we were going to run out!"

"Run out of monsters?" Isaac asked, puzzled.

"Yeah, out of monsters. I love being Huckleberry, and I love Raspberry Riot. There's not much reason for either if there aren't any monsters to fight, is there?"

"That's a bit of a gloomy outlook, don't you think?"

"Not gloomy, no." said Huckleberry. "It's a matter of practicality. There's never been a 'peacetime' magical girl crew. And people like us lots, but I don't think they like us enough to make us the first."

->serena_choices



==serena_confrontation===

"What do you mean?" asked Huckleberry.

"Well, you have 'Magical' in your name, do you not do magic?"

"I'm not sure what you mean, all of what we do is magic." Huckleberry stated boredly.

* [Push forward.] "Of course, right. I should be more specific. I mean more general magic, like the kind that can summon or create monsters?"
    ->serena_confrontation2
* [Stop Pushing.] "Of course, of course. That was a silly question."
    -> serena_choices



==serena_confrontation2===

"What are you implying?" asked Huckleberry, sounding insulted.

"You know magic, and only being with magic can make monsters. The last magical being who was willing to make monsters was King Goregamesh, who you and Raspberry Riot defeated. Instead of celebrating the defeat of Goregamesh, you were busy worrying about what to fight next, because you don't seem to have any friends or meaningful personal connections outside of Raspberry Riot."

Huckleberry started to protest, but Isaac interrupted her: "Look, it doesn't paint a good picture and I know I'm pretty sure that you're the one summoning monsters, all things considered."

Huckleberry glared at Isaac with a mixture of anger and frustration. She spent a moment considering something, then looked Isaac in his eyes. "Ok, and? What are you going to do about it?" she asked cooly.

"I haven't decided yet," said Isaac, "but I'm a reporter, not a cop. So I suppose I either report what I've found, or I don't."

Isaac continued: "Now, I can imagine you don't want me to report on this, as I can't imagine that ends well for you. And in any other circumstance, I'd publish this in a heartbeat: stories like this come along once in a lifetime."

Isaac slumped back in Lee's desk chair, and wiped his forehead tiredly. "But you're so young. I'm not going to pretend I don't understand what you're doing, but you're putting the city in danger. So give me a reason not to publish this. I can just write the nice set of interviews I came here to get, but only if..."

"Only if what?" asked Huckleberry, her expression unreadable.

"I haven't decided yet." Isaac said.

"How helpful." sassed Huckleberry. "Are we done here?"

"If you want to be," said Isaac.

"Great, I'll be on my way then." Huckleberry strutted towards the door of Lee's room, but turned back to look at Isaac in the threshold.

"I hope you make the right decision." she said coldly, glaring at Isaac surrounded by a dim teal glow and an aura of power.

She stormed out of the room, hoping that he hadn't noticed her hand trembling on her staff.

He had.

->selection




==serena_wrapup===

Isaac continued, "It was a pleasure to meet you, and to speak with you!"

"Likewise!" said Huckleberry. She shakes the reporter's hand, and struts out of the room.

-> selection



